options {
    JAVA_UNICODE_ESCAPE = true;
    ERROR_REPORTING = true;
    STATIC = false;
    JDK_VERSION = "1.7";
    BUILD_PARSER = true;
    IGNORE_CASE = true;
}



PARSER_BEGIN(Assembler)
package org.z64sim.assembler;
import java.io.StringReader;
import java.io.Reader;
import java.util.List;
import java.util.ArrayList;

public class Assembler {

    /** To support error recovery */
    public List<ParseException> syntaxErrors = new ArrayList<ParseException>();

    /**
     * A String-based constructor for ease of use
     *
     * @param s The program to be assembled
     */
    public Assembler(String s) {
        this((Reader)(new StringReader(s)));
    }

    /**
     * Error recovery function
     *
     * @param ex The exception keeping the parse error
     * @param kind The token to be reached so as to continue parsing
     */
    void error_recover(ParseException ex, int kind) {
        syntaxErrors.add(ex);
        Token t;
        do {
            t = getNextToken();
        } while (t.kind != kind);
    }

}
PARSER_END(Assembler)


/*********
 * LEXER *
 *********/


/* No SKIP charachters: they are hidden to the Lexer interface, and
 * are rather needed for syntax highlighting. Might have an effect on performance
 * nevertheless...
 */
SPECIAL_TOKEN :
{ <WHITESPACE:
  " "
| "\t"
| "\f">
}

TOKEN :
{
<NEWLINE:
("\n"
| "\r"
| ";")+ >
}

TOKEN :
{ 
  <COMMENT: ( "#" (~["\n","\r"])* ("\n"|"\r"|"\r\n") ) |
            ( "/*" (~["*"])* "*" ("*" | ~["*","/"] (~["*"])* "*")* "/") >
}


/** 
 * Regular tokens start here 
 */



/** Keywords */
TOKEN:
{ <LOCATION_COUNTER: ".">
| <ORG: ".org">
| <DATA_SECTION: ".data">
| <CODE_SECTION: ".text">
| <BSS_SECTION: ".bss">
| <PROGRAM_END: ".end">
| <EQU_ASSIGN: ".equ">
| <BYTE_ASSIGN: ".byte">
| <WORD_ASSIGN: ".word">
| <LONG_ASSIGN: ".long">
| <QUAD_ASSIGN: ".quad">
| <ASCII_ASSIGN: ".ascii"> 
| <FILL_ASSIGN: ".fill">
| <COMM_ASSIGN: ".comm">
| <DRIVER: (".driver" | ".handler")>
}

/** Numeric/Arithmetic/Separator stuff */
TOKEN:
{ <CONSTANT: "$">
| <INTEGER: <DEC> | <HEX> | <BIN> | <FLONUM>>
|   <#DEC: (["0"-"9"])+>
|   <#HEX: "0x"(["0"-"9","a"-"f"])+>
|   <#BIN: "0b"(["0","1"])+>
| <FLONUM: "0e"("+"|"-")? (["0"-"9"])* ("." (["0"-"9"])+ )? ( "e" ("+"|"-")? (["0"-"9"])+ )? >
| <ASSIGN: "=">
| <PLUS: "+">
| <MINUS: "-">
| <TIMES: "*">
| <DIVIDE: "/">
| <LBRACE: "(">
| <RBRACE: ")">
| <COMMA: ",">
}

/** Registers */
TOKEN:
{ <REG_8: "%al" | "%cl" | "%dl" | "%bl">
| <REG_16: "%ax" | "%bx" | "%cx" | "%dx" | "%sp" | "%bp" | "%si" | "%di">
| <REG_32: "%eax" | "%ebx" | "%ecx" | "%edx" | "%esp" | "%ebp" | "%esi" | "%edi">
| <REG_64: "%rax" | "%rbx" | "%rcx" | "%rdx" | "%rsp" | "%rbp" | "%rsi" | "%rdi" | "%r8" | "%r9" | "%r10" | "%r11" | "%r12" | "%r13" | "%r14" | "%r15">
}

/** Instructions */
TOKEN:
{ <INSN_0: ( "movs" 
           | "stos" )("b"|"w"|"l"|"q")?>
| <INSN_0_WQ: ("pushf" 
             | "popf" )("w"|"l"|"q")?>
| <INSN_0_NOSUFF:  "ret" 
                 | "clc" 
                 | "clp" 
                 | "clz" 
                 | "cls" 
                 | "cli" 
                 | "cld" 
                 | "clo" 
                 | "stc" 
                 | "stp"
                 | "stz" 
                 | "sts" 
                 | "sti"
                 | "std" 
                 | "sto" 
                 | "hlt" 
                 | "nop">
| <INSN_1_S: "int">
| <INSN_LEA: "lea"("b"|"w"|"l"|"q")?>   
| <INSN_1_E: ("push"
           | "pop"
           | "neg"
           | "not")("b"|"w"|"l"|"q")?>
| <INSN_SHIFT: ("sal"
            | "shl"
            | "sar"
            | "shr"
            | "rcl"
            | "rol"
            | "ror")("b"|"w"|"l"|"q")?>
| <INSN_1_M: ("jc"
            | "jp"
            | "jz"
            | "js"
            | "jo"
            | "jnc"
            | "jnp"
            | "jnz"
            | "jns"
            | "jno")>
| <INSN_JC: ("jmp"
           | "call")("b"|"w"|"l"|"q")?>
| <INSN_B_E: ("mov"
            | "add"
            | "sub"
            | "adc"
            | "sbb"
            | "cmp"
            | "test"
            | "and"
            | "or"
            | "xor")("b"|"w"|"l"|"q")?>
| <INSN_EXT: ("movs" | "movz")("bw"|"bl"|"bq"|"wl"|"wq"|"lq")>
| <INSN_IN: "in"("b"|"w"|"l"|"q")?>
| <INSN_OUT: "out"("b"|"w"|"l"|"q")?>
| <INSN_IO_S: ("ins"
           | "outs")("b"|"w"|"l")>
| <IRET: "iret">
}

/** Label/Text/Ascii stuff */
TOKEN:
{ <LABEL: <LABEL_NAME> ":">
| <LABEL_NAME: ([".","_","-","a"-"z","0"-"9"])+ >
| <STRING_LITERAL: "\"" ( ~["\"","\\","\n","\r"] | "\\" ( ["n","t","b","r","f","\\","\'","\""] | ["0"-"7"] (["0"-"7"])?  | ["0"-"3"] ["0"-"7"] ["0"-"7"] | ( ["\n","\r"] | "\r\n")))* "\"">
}


TOKEN:
{
    /* Anything else is considered to be and error character.
     * Only one character must be matched because the (~[])+
     * would be the longest match so it would be always
     * preferred. The ERROR clause must be the last
     * in the grammar specification so that other one-char
     * clauses are matched preferably.
     * This is REQUIRED for correct online syntax highlighting,
     * but makes actual parsing a bit more difficult.
     */
    <ERROR: ~[] >
}






/****************
 * PARSER RULES *
 ****************/


void Program() :
{}
{ LocationCounter() <NEWLINE>
  Data()
  Code()
  Drivers()
  <PROGRAM_END>
  (<NEWLINE>)* /* This is to consume newlines at the end of file */
  <EOF>
}


void Data() :
{}
{
    (   (<DATA_SECTION> | <BSS_SECTION>) <NEWLINE>
        ((
              LocationCounter()
            | <EQU_ASSIGN> <LABEL_NAME> "," Expression()
            | <LABEL_NAME> <ASSIGN> Expression()
            | <LABEL>
              (
                   ( <BYTE_ASSIGN> | <WORD_ASSIGN> | <LONG_ASSIGN> | <QUAD_ASSIGN> ) Expression() ("," Expression())*
                 | <ASCII_ASSIGN> <STRING_LITERAL>
              )
            | <FILL_ASSIGN> Expression() ( "," Expression() ( "," Expression() )? )?
            | <COMM_ASSIGN> <LABEL_NAME> "," Expression()
        ) <NEWLINE>)*
    )+
}

void Code() :
{}
{ 
    (
        <DRIVER> <INTEGER> <NEWLINE>
        ( Statement() )*
    )+
}

void Drivers() :
{}
{ 
    (
        <CODE_SECTION> <NEWLINE>
        ( Statement() )*
        <IRET>
    )+
}


void Statement() :
{}
{
  Label() <NEWLINE> 
| LocationCounter() <NEWLINE>
| Instruction() <NEWLINE> 
| <NEWLINE>
}

void LocationCounter() :
{}
{ 
    try {

        /* ACTUAL RULE */
        (   <LOCATION_COUNTER> <ASSIGN>
          | <ORG> ("," <INTEGER> )?
        )
        <INTEGER>

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}


void Instruction() :
{}
{
    try {

        /* ACTUAL RULE */
        <INSN_0>
      | <INSN_0_WQ>
      | <INSN_0_NOSUFF>
      | <INSN_1_S> <INTEGER>
      | <INSN_1_E> FormatE()
      | <INSN_SHIFT> ( FormatK() <COMMA> )? FormatG()
      | <INSN_1_M> FormatM()
      | <INSN_JC> ( ( <TIMES> FormatG() ) | FormatM() )
      | <INSN_B_E> FormatB() <COMMA> FormatE()
      | <INSN_EXT> FormatE() <COMMA> FormatG()
      | <INSN_LEA> FormatE() <COMMA> FormatE()
      | <INSN_IN> <REG_16> <COMMA> Register() /* programmatically check for registers */
      | <INSN_OUT> Register() <COMMA> <REG_16> /* programmatically check for registers */
      | <INSN_IO_S>

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}




void Label() :
{}
{ 
    try {

        /* ACTUAL RULE */
        <LABEL> 

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}



void Register() :
{}
{
    try {

        /* ACTUAL RULE */
        <REG_8> | <REG_16> | <REG_32> | <REG_64>

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}


void Addressing() :
{}
{ 
    try {

        /* ACTUAL RULE */
        ( LOOKAHEAD(2) Expression() )?
        ( <LBRACE>
         (<REG_8> | <REG_16> | <REG_32> | <REG_64>)
            ("," (<REG_8> | <REG_16> | <REG_32> | <REG_64>) "," <INTEGER>)?
        <RBRACE> )?

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}


void FormatE() :
{}
{ 
    try {

        /* ACTUAL RULE */
        Register() | Addressing()

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}



void FormatK() :
{}
{ 
    try {

        /* ACTUAL RULE */
        <INTEGER>

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}



void FormatG() :
{}
{
    try {

        /* ACTUAL RULE */
        Register()

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}


/* Both label and direct address */
void FormatM() : 
{}
{
    try {

        /* ACTUAL RULE */
         Label()
       | Addressing()

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}



void FormatB() :
{}
{
    try {

        /* ACTUAL RULE */
          ConstantExpression()
        | FormatG()
        | FormatM()

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}


/***************************************************/
/* Rules to handle simple expressions in constants */
/***************************************************/

void ConstantExpression() :
{}
{
    try {

        /* ACTUAL RULE */
        <CONSTANT>
        Expression()

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    } 
}


void Expression() :
{}
{
    try {

        /* ACTUAL RULE */
        Term() ( <PLUS> Term() | <MINUS> Term() )*

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    } 
}

void Term() :
{}
{
    try {

        Primary() ( <TIMES> Primary() | <DIVIDE> Primary() )*

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}

void Primary() :
{}
{
    try {

        <INTEGER>
      | <LOCATION_COUNTER>
      | <LABEL_NAME>
      | <LBRACE> Expression() <RBRACE>
      | <MINUS> Primary()

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}
