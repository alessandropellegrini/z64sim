/**
 *
 * SPDX-FileCopyrightText: 2015-2023 Alessandro Pellegrini <a.pellegrini@ing.uniroma2.it>
 * SPDX-License-Identifier: GPL-3.0-only
 */
options {
    JAVA_UNICODE_ESCAPE = true;
    ERROR_REPORTING = true;
    STATIC = false;
    JDK_VERSION = "1.8";
    BUILD_PARSER = true;
    IGNORE_CASE = true;
}


PARSER_BEGIN(Assembler)
package it.uniroma2.pellegrini.z64sim.assembler;

import it.uniroma2.pellegrini.z64sim.controller.exceptions.ProgramException;
import it.uniroma2.pellegrini.z64sim.isa.operands.*;
import it.uniroma2.pellegrini.z64sim.isa.registers.*;
import it.uniroma2.pellegrini.z64sim.isa.instructions.*;
import it.uniroma2.pellegrini.z64sim.model.*;
import it.uniroma2.pellegrini.z64sim.controller.*;

import java.io.StringReader;
import java.io.Reader;
import java.util.List;
import java.util.ArrayList;


public class Assembler {

    // The actual Program we are assembling
    private Program program = new Program();

    /** To support error recovery */
    private List<String> syntaxErrors = new ArrayList<>();

    /**
     * A String-based constructor for ease of use
     *
     * @param s The program to be assembled
     */
    public Assembler(String s) {
        this((Reader)(new StringReader(s)));
    }

    /**
     * Return the assembled program, e.g. for displaying or simulation
     * @return the assembled program
     */
    public Program getProgram() {
        return this.program;
    }

    /**
     * Return found errors, e.g. to show them in the editor
     * @return list of ParseExceptions
     */
    public List<String> getSyntaxErrors() {
        return this.syntaxErrors;
    }

    /**
     * Error recovery function
     *
     * @param ex The exception keeping the parse error
     * @param kind The token to be reached so as to continue parsing
     */
    private void error_recover(ParseException ex, int kind) {
        syntaxErrors.add(ex.getMessage());
        Token t;
        do {
            t = getNextToken();
        } while (t.kind != kind && t.kind != EOF);
    }

    private int getSourceSuffixSize(String mnemonic) throws ParseException {
        String suffix = mnemonic.substring(mnemonic.length() - 2, mnemonic.length() - 1);

        switch(suffix) {
            case "b":
                return 1;
            case "w":
                return 2;
            case "l":
                return 4;
        }
        throw new ParseException("Zero/Sign extension with wrong source prefix");
    }

    private int getSuffixSize(String mnemonic) {
        String suffix = mnemonic.substring(mnemonic.length() - 1);

        // Some instructions would allow no suffix, but they end with a char which
        // could be interpreted as a suffix by this function. These cases are
        // handled here, before checking the suffix.
        switch(mnemonic) {
            case "call":
                return 8;
            case "shl":
            case "sal":
            case "rcl":
            case "rol":
            case "sub":
            case "sbb":
                return -1;
        }

        switch(suffix) {
            case "b":
                return 1;
            case "w":
                return 2;
            case "l":
                return 4;
            case "q":
                return 8;
        }
        return -1; // No suffix given, depends on the operands
    }

    private String stripSuffix(String mnemonic) {
        // Some instructions would allow no suffix, but they end with a char which
        // could be interpreted as a suffix by this function. These cases are
        // handled here, before checking the suffix.
        switch(mnemonic) {
            case "call":
            case "shl":
            case "sal":
            case "rcl":
            case "rol":
            case "sub":
            case "sbb":
                return mnemonic;
        }

        if(getSuffixSize(mnemonic) != -1)
            return mnemonic.substring(0, mnemonic.length() - 1);
        return mnemonic;
    }

    private int stringToInt(String s) throws ParseException {

        if(s.length() >= 2) {
            if(s.startsWith("0e"))
                throw new ParseException("FLONUMS are still not supported"); // TODO

            if(s.startsWith("0b"))
                return Integer.parseInt(s.substring(2), 2);
        }

        return Integer.decode(s);
    }

    // Converts a long to a byte[] vector, representing elements as little endians
    private byte[] dataToByte(int elementSize, long value) throws ParseException {
        byte[] data = new byte[elementSize];

        switch(elementSize) {
            case 1:
                data[0] = (byte)(value);
                break;
            case 2:
                data[1] = (byte)(value >> 8);
                data[0] = (byte)(value);
                break;
            case 4:
                data[3] = (byte)(value >> 24);
                data[2] = (byte)(value >> 16);
                data[1] = (byte)(value >> 8);
                data[0] = (byte)(value);
                break;
            case 8:
                data[7] = (byte)(value >> 56);
                data[6] = (byte)(value >> 48);
                data[5] = (byte)(value >> 40);
                data[4] = (byte)(value >> 32);
                data[3] = (byte)(value >> 24);
                data[2] = (byte)(value >> 16);
                data[1] = (byte)(value >> 8);
                data[0] = (byte)(value);
                break;
            default:
                throw new ParseException("Runtime error in the assembler");
        }

        return data;
    }

    private byte[] getFilledMemoryArea(int repeat, byte size, long value) throws ParseException {
        byte[] fill = new byte[repeat*size];
        // convert value to bytes
        try {
            byte[] valueBytes = dataToByte(size, value);
            for(int i = 0; i < repeat; i++) {
                for (int j = 0; j < size; j++) {
                    fill[(i * size) + j] = valueBytes[j];
        }
            }
        }  catch (ParseException ex) {
            throw new ParseException("Parser cannot fill memory area: A valid size must be 1,2,4,8");
        }

        return fill;
    }
}
PARSER_END(Assembler)


/*********
 * LEXER *
 *********/


/* No SKIP charachters: they are hidden to the Lexer interface, and
 * are rather needed for syntax highlighting. Might have an effect on performance
 * nevertheless...
 */
SPECIAL_TOKEN :
{ <WHITESPACE:
  " "
| "\t"
| "\f">
}

TOKEN :
{
<NEWLINE:
("\n"
| "\r"
| ";")+ >
}

TOKEN :
{
  <COMMENT: ( "#" (~["\n","\r"])* ("\n"|"\r"|"\r\n") ) >
}


/**
 * Regular tokens start here
 */



/** Keywords */
TOKEN:
{ <LOCATION_COUNTER: ".">
| <ORG: ".org">
| <DATA_SECTION: ".data">
| <CODE_SECTION: ".text">
| <BSS_SECTION: ".bss">
| <EQU_ASSIGN: ".equ">
| <BYTE_ASSIGN: ".byte">
| <WORD_ASSIGN: ".word">
| <LONG_ASSIGN: ".long">
| <QUAD_ASSIGN: ".quad">
| <ASCII_ASSIGN: ".ascii">
| <FILL_ASSIGN: ".fill">
| <COMM_ASSIGN: ".comm">
| <DRIVER: (".driver" | ".handler")>
}

/** Numeric/Arithmetic/Separator stuff */
TOKEN:
{ <CONSTANT: "$">
| <INTEGER: <DEC> | <HEX> | <BIN> | <FLONUM>>
|   <#DEC: (["0"-"9"])+>
|   <#HEX: "0x"(["0"-"9","a"-"f"])+>
|   <#BIN: "0b"(["0","1"])+>
| <FLONUM: "0e"("+"|"-")? (["0"-"9"])* ("." (["0"-"9"])+ )? ( "e" ("+"|"-")? (["0"-"9"])+ )? >
| <ASSIGN: "=">
| <PLUS: "+">
| <MINUS: "-">
| <TIMES: "*">
| <DIVIDE: "/">
| <LBRACE: "(">
| <RBRACE: ")">
| <COMMA: ",">
}

/** Registers */
TOKEN:
{ <REG_8: "%al" | "%cl" | "%dl" | "%bl" | "%spl" | "%bpl" | "%sil" | "%dil" | "%r8b" | "%r9b" | "%r10b" | "%r11b" | "%r12b" | "%r13b" | "%r14b" | "%r15b" >
| <REG_16: "%ax" | "%bx" | "%cx" | "%dx" | "%sp" | "%bp" | "%si" | "%di" | "%r8w" | "%r9w" | "%r10w" | "%r11w" | "%r12w" | "%r13w" | "%r14w" | "%r15w" >
| <REG_32: "%eax" | "%ebx" | "%ecx" | "%edx" | "%esp" | "%ebp" | "%esi" | "%edi" | "%r8d" | "%r9d" | "%r10d" | "%r11d" | "%r12d" | "%r13d" | "%r14d" | "%r15d" >
| <REG_64: "%rax" | "%rbx" | "%rcx" | "%rdx" | "%rsp" | "%rbp" | "%rsi" | "%rdi" | "%r8" | "%r9" | "%r10" | "%r11" | "%r12" | "%r13" | "%r14" | "%r15" >
}

/** Instructions */
TOKEN:
{ <INSN_0: ( "movs"
           | "stos" )("b"|"w"|"l"|"q")?>
| <INSN_0_WQ: ("pushf"
             | "popf" )("w"|"l"|"q")?>
| <INSN_0_NOSUFF:  "ret"("q")?
                 | "clc"
                 | "clp"
                 | "clz"
                 | "cls"
                 | "cli"
                 | "cld"
                 | "clo"
                 | "stc"
                 | "stp"
                 | "stz"
                 | "sts"
                 | "sti"
                 | "std"
                 | "sto"
                 | "hlt"
                 | "nop">
| <INSN_1_S: "int">
| <INSN_LEA: "lea"("b"|"w"|"l"|"q")?>
| <INSN_1_B: ("push"
           | "pop"
           | "neg"
           | "not")("b"|"w"|"l"|"q")?>
| <INSN_SHIFT: ("sal"
            | "shl"
            | "sar"
            | "shr"
            | "rcl"
            | "rcr"
            | "rol"
            | "ror")("b"|"w"|"l"|"q")?>
| <INSN_BT: ("bt")("b"|"w"|"l"|"q")?>
| <INSN_1_M: ("jc"
            | "jp"
            | "jz"
            | "js"
            | "jo"
            | "jnc"
            | "jnp"
            | "jnz"
            | "jns"
            | "jno")>
| <INSN_JC: ("jmp"
           | "call")("b"|"w"|"l"|"q")?>
| <INSN_B_E: ("mov"
            | "add"
            | "sub"
            | "adc"
            | "sbb"
            | "cmp"
            | "test"
            | "and"
            | "or"
            | "xor")("b"|"w"|"l"|"q")?>
| <INSN_EXT: ("movs" | "movz")("bw"|"bl"|"bq"|"wl"|"wq"|"lq")>
| <INSN_IN: "in"("b"|"w"|"l"|"q")?>
| <INSN_OUT: "out"("b"|"w"|"l"|"q")?>
| <INSN_IO_S :("ins"
           | "outs")("b"|"w"|"l")>
| <IRET: "iret"("q")?>
}

/** Label/Text/Ascii stuff */
TOKEN:
{ <LABEL: <LABEL_NAME> ":">
| <LABEL_NAME: ([".","_","a"-"z","0"-"9"])+ >
| <STRING_LITERAL: "\"" ( ~["\"","\\","\n","\r"] | "\\" ( ["n","t","b","r","f","\\","\'","\""] | ["0"-"7"] (["0"-"7"])?  | ["0"-"3"] ["0"-"7"] ["0"-"7"] | ( ["\n","\r"] | "\r\n")))* "\"">
}


TOKEN:
{
    /* Anything else is considered to be and error character.
     * Only one character must be matched because the (~[])+
     * would be the longest match so it would be always
     * preferred. The ERROR clause must be the last
     * in the grammar specification so that other one-char
     * clauses are matched preferably.
     * This is REQUIRED for correct online syntax highlighting,
     * but makes actual parsing a bit more difficult.
     */
    <ERROR: ~[] >
}






/****************
 * PARSER RULES *
 ****************/


/**
 * This is the Axiom of the parser
 */
void Program() :
{}
{
    try {
        (<COMMENT> | <NEWLINE>)*
        LocationCounter() (<COMMENT> | <NEWLINE>)+
        (Data())?
        Code()
        Drivers()
        <EOF>

        {   // Finalize the program
            try {
                this.program.finalizeProgram();
            } catch(ProgramException e) {
                this.syntaxErrors.add(e.getMessage());
            }
        }
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}


void Data() :
{
    Token t1, t2;
    long value = -1;
    long repeat, size = -1; // Used for fill assignments
    int addr;
    int elementSize;
    byte[] data;
    String l;
}
{
    try {
        (   <DATA_SECTION>
            (
                <COMMENT> | <NEWLINE>
                |  LocationCounter()
                | <EQU_ASSIGN> t1 = <LABEL_NAME> "," value = Expression()
                  {
                    try {
                        this.program.addEqu(t1.image, value);
                    } catch(ProgramException e) {
                        throw new ParseException(e.getMessage());
                    }
                  }
                | t1 = <LABEL_NAME> <ASSIGN> value = Expression()
                  {
                    try {
                        this.program.addEqu(t1.image, value);
                    } catch(ProgramException e) {
                        throw new ParseException(e.getMessage());
                    }
                  }
                | (t1 = <LABEL>
                  {
                        // Add the label, remove trailing ':'
                       l = t1.image.substring(0, t1.image.length()-1);
                       this.program.newLabel(l, new MemoryPointer(this.program.getLocationCounter()));
                  }
                  )+
                  (
                       ( <BYTE_ASSIGN> {elementSize = 1;}
                       | <WORD_ASSIGN> {elementSize = 2;}
                       | <LONG_ASSIGN> {elementSize = 4;}
                       | <QUAD_ASSIGN> {elementSize = 8;} )
                          value = Expression()
                          { data = dataToByte(elementSize, value);
                            addr = this.program.addData(data);
                          }
                       ("," value = Expression()
                          {
                            data = dataToByte(elementSize, value);
                            addr = this.program.addData(data);
                          }
                       )*
                     | <ASCII_ASSIGN> t2 = <STRING_LITERAL>
                       {
                         // Put data in memory
                         byte[] str = t2.image.substring(1, t2.image.length()).getBytes();
                         str[str.length - 1] = 0; // Replace last " with \0
                         addr = this.program.addData(str);

                        // Add the label
                        this.program.newLabel(t1.image, new MemoryPointer(addr));
                       }
                     | <FILL_ASSIGN> repeat = Expression() ( "," size = Expression() ( "," value = Expression() )? )?
                       {
                          // Value is the third optional parameter
                          if(value == -1)
                              value = 0;

                          // Size is the second optional parameter
                          if(size == -1)
                              size = 1;

                          // Create 'repeat' DataElements set to value
                          addr = this.program.addData(getFilledMemoryArea((int)repeat, (byte)size, value));

                          // Add the label
                          this.program.newLabel(t1.image, new MemoryPointer(addr));
                       }
                  )
                | <COMM_ASSIGN> t1 = <LABEL_NAME> "," size = Expression()
                  {
                    // .comm assigns to zero
                    addr = this.program.addData(getFilledMemoryArea((int)size, (byte)1, 0) );
                    this.program.newLabel(t1.image, new MemoryPointer(addr));
                  }
            )+
        )+
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
    {this.program.finalizeData();}
}

void Code() :
{
    Instruction i;
}
{
    try {
        <CODE_SECTION>
        {
            this.program.textSectionStart(this.program.getLocationCounter());
        }
        (<COMMENT> | <NEWLINE>)+
        (
            i = Statement()
            {if(i != null) {
                //The size of the MemoryElement is added by the Instruction classes
                this.program.addInstruction(i);
            }
}
        )*
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}

Instruction Drivers() :
{
    Token t;
}
{
    try {
        (
            <DRIVER> (t=<INTEGER> | t=<LABEL_NAME>)
            {
                if(t.kind == INTEGER) {
                    this.program.newDriver(Integer.parseInt(t.image), this.program.getLocationCounter());
                } else if(t.kind == LABEL_NAME) { // this can only be an equ
                    if(this.program.getEqu(t.image) == null)
                        throw new ParseException("Line " + t.beginLine + ": " + t.image + " undefined");
                    this.program.newDriver(this.program.getEqu(t.image).intValue(), this.program.getLocationCounter());
                } else {
                    throw new ParseException("Line " + t.beginLine + ": Invalid driver definition");
                }
            }
            (<COMMENT> | <NEWLINE>)*
            ( Statement() )+
            t = <IRET>
            {
                this.program.addInstruction(new InstructionClass5(t.image, null));
            }
            (<COMMENT> | <NEWLINE>)+
        )*
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    } finally {
        return null; // otherwise javaCC throws exceptions
    }
}

Instruction Statement() :
{
    String label;
    Instruction i = null;
}
{
    try {
        (
          label = Label()
            {this.program.newLabel(label, new MemoryPointer(this.program.getLocationCounter()));}
          | LocationCounter()
          | i = Instruction()
        )
        (<COMMENT> | <NEWLINE>)*
        { return i; }

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return null; // otherwise javaCC throws exceptions
    }
}

void LocationCounter() :
{
    Token t, t2 = null;
    int newLocationCounter;
}
{
    try {
        (   <LOCATION_COUNTER> <ASSIGN> t = <INTEGER>
          | <ORG> t = <INTEGER> ("," t2 = <INTEGER> )?
        )
        {
            newLocationCounter = stringToInt( t.image );

            // if t2 != null, we have a fill assignment
            if(t2 != null) {
                long value = stringToInt( t2.image );
                long size = newLocationCounter - this.program.getLocationCounter();
                this.program.addData(getFilledMemoryArea((int)size, (byte)1, value));

            } else {
                try {
                    // It's in the else branch as the addMemoryElement above already
                    // increases the location counter's value by size
                    program.setLocationCounter( newLocationCounter );
                } catch (ProgramException ex) {
                    throw new ParseException("Line " + t.beginLine + ": " + ex.getMessage());
                }
            }
        }
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
    }
}

Instruction Instruction() :
{
    Token t = null, t2 = null, l;
    int size;
    int sizeExt;
    String mnemonic;
    Operand op1 = null, op2 = null;
    int i = -1;
    Instruction insn = null;
}
{
    try {
    (
        t = <INSN_0>
        {
            size = getSuffixSize( t.image );
            insn = new InstructionClass1(stripSuffix(t.image), null, null, size);
        }
      |
        t = <INSN_0_WQ>
        {
            size = getSuffixSize( t.image );
            insn = new InstructionClass1(stripSuffix(t.image), null, null, size);
        }
      |
        t = <INSN_0_NOSUFF>
        {
            // ret is class 5, with no actual operand
            if(t.image.equals("ret") || t.image.equals("retq"))
                insn = new InstructionClass5(t.image, null);

            // hlt, nop are class 0
            else if(t.image.equals("hlt") || t.image.equals("nop"))
                insn = new InstructionClass0(t.image, null); // null is for the idn in a non-int instruction

            // others are all class 4
            else
                insn = new InstructionClass4(t.image);
        }
      |
        t = <INSN_1_S> op1 = ConstantExpression()
        { insn = new InstructionClass0(t.image, (OperandImmediate) op1); }
      |
        t = <INSN_1_B>  { size = getSuffixSize(t.image); }
        op1 = FormatB(size)
        {
            mnemonic = stripSuffix(t.image);

            // Sanity check
            if(size == -1) // Size is given by the operand
                size = op1.getSize();
            if(op1 != null && op1.getSize() != size)
                throw new ParseException("Line " + t.beginLine + ": Operand size and instruction suffix mismatch");

            // push and pop are class 1
            if(mnemonic.equals("push") || mnemonic.equals("pop"))
                insn = new InstructionClass1(mnemonic, op1, null, -1);

            // neg and not are class 2
            else
                insn = new InstructionClass2(mnemonic, op1, null);
        }
      |
        t = <INSN_SHIFT> ( i = FormatK() <COMMA> )? op1 = FormatG()
        {
            // Shift instructions belong all to class 3
            insn = new InstructionClass3(stripSuffix(t.image), i, (OperandRegister)op1);
        }
      |
        t = <INSN_BT> { size = getSuffixSize( t.image ); }
            i = FormatK() <COMMA> op2 = FormatE(size)
        {
            op1 = new OperandImmediate(i);

            // Shift instructions belong all to class 3
            insn = new InstructionClass2(stripSuffix(t.image), op1, op2);
        }
      |
        t = <INSN_1_M> { size = getSuffixSize( t.image ); }
        op1 = FormatM(size)
        {
            // They all belong to class 6
            insn = new InstructionClass6( stripSuffix(t.image), (OperandMemory)op1);
        }
      |
        t = <INSN_JC> { size = /*getSuffixSize( t.image )*/-1; }
        ( ( <TIMES> op1 = FormatG() ) | op1 = FormatM(size) )
        {

//            if(size == -1) // Size is given by the operand
//                size = op1.getSize();
//            else if(op1 != null && op1.getSize() != size) // Sanity check
//                throw new ParseException("Line " + t.beginLine + ": Operand size and instruction suffix mismatch.");

            // They all belong to class 5
            insn = new InstructionClass5( stripSuffix(t.image), op1);
        }
      |
        t = <INSN_B_E>  { size = getSuffixSize( t.image ); }
        op1 = FormatB(size) <COMMA> op2 = FormatE(size)
        {
            mnemonic = stripSuffix(t.image);

            // Sanity check
            if(size == -1) { // Size is given by the operand
                if(op1 instanceof OperandRegister)
                    size = op1.getSize();
                else if(op2 instanceof OperandRegister)
                    size = op2.getSize();

                if(op1 instanceof OperandImmediate)
                    op1.setSize(size);
                if(op1 instanceof OperandMemory)
                    op1.setSize(size);
                if(op2 instanceof OperandMemory)
                    op2.setSize(size);
            }
            if( (op1 instanceof OperandRegister && op1.getSize() != size) || ( op2 instanceof OperandRegister && op2.getSize() != size))
                throw new ParseException("Line " + t.beginLine + ": Operand size and instruction suffix mismatch, or size not specified.");

            // mov is class 1
            if(mnemonic.equals("mov"))
                insn = new InstructionClass1(mnemonic, op1, op2, -1);
            // others are all class 2
            else
                insn = new InstructionClass2(mnemonic, op1, op2);
        }
      |
        t = <INSN_EXT>  { size = getSourceSuffixSize( t.image ); }
        op1 = FormatE(size) <COMMA> op2 = FormatG()
        {
            sizeExt = getSuffixSize(t.image);
            mnemonic = stripSuffix(stripSuffix(t.image)); // This instruction has two suffixes

            // Sanity check
            if(op1 == null || op2 == null)
                throw new ParseException("Line " + t.beginLine + ": Invalid operands.");
            if(op1.getSize() != size || op2.getSize() != sizeExt)
                throw new ParseException("Line " + t.beginLine + ": Operand size mismatch.");

            if(size >= sizeExt)
                throw new ParseException("Line " + t.beginLine + ": Wrong suffices for extension: cannot extend from" + size + " to " + sizeExt);

            insn = new InstructionClass1(mnemonic+"X", op1, op2, -1);
        }
      |
        t = <INSN_LEA> { size = getSuffixSize( t.image ); }
        op1 = FormatM(size) <COMMA> op2 = FormatG()
        {
            mnemonic = stripSuffix(t.image);

            // Sanity check
            if(op1 == null || op2 == null)
                throw new ParseException("Line " + t.beginLine + ": Invalid operands.");
            if(size == -1) { // Size is given by the destination operand
                size = op2.getSize();
                op1.setSize(size);
            }
            if(op1.getSize() != size || op2.getSize() != size)
                throw new ParseException("Line " + t.beginLine + ": Operand size and instruction suffix mismatch.");

            insn = new InstructionClass1(mnemonic, op1, op2, -1);
        }
      |
        t = <INSN_IN>
            (t2 = <REG_16> | op1 = ConstantExpression())
            <COMMA> op2 = Register() /* programmatically check for registers */
        {
            size = getSuffixSize( t.image );
            mnemonic = stripSuffix(t.image);

            if(size == -1)
                size = op2.getSize();

            if(((OperandRegister)op2).getRegister() != Register.RAX)
                throw new ParseException("Line " + t.beginLine + ": in instruction must use RAX as destination operand");

            if(t2 != null && !t2.image.equals("%dx"))
                throw new ParseException("Line " + t.beginLine + ": in instruction must use DX as source register");

            insn = new InstructionClass7(mnemonic, size, op1);
        }
      |
        t = <INSN_OUT>
            op1 = Register() <COMMA>
            (t2 = <REG_16> | op2 = ConstantExpression()) /* programmatically check for registers */
        {
            size = getSuffixSize( t.image );
            mnemonic = stripSuffix(t.image);

            if(size == -1)
                size = op1.getSize();

            if(((OperandRegister)op1).getRegister() != Register.RAX)
                throw new ParseException("Line " + t.beginLine + ": out instruction must use RAX as source operand");

            if(t2 != null && !t2.image.equals("%dx"))
                    throw new ParseException("Line " + t.beginLine + ": out instruction must use DX as destination register");

            insn = new InstructionClass7(mnemonic, size, op2);
        }
      |
        t = <INSN_IO_S>
        {
            size = getSuffixSize( t.image );
            mnemonic = stripSuffix(t.image);

            if(size == -1)
                throw new ParseException("Line " + t.beginLine + ": Wrong size suffix for instruction " + mnemonic);

            insn = new InstructionClass7(mnemonic, size, null);
        }
    )
    { return insn; }
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return null; // otherwise javaCC gets angry
    }
}




String Label() :
{
    Token t;
}
{
    try {
        t = <LABEL>
        {
          // Remove trailing ':'
          return t.image.substring(0, t.image.length()-1);
        }
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return "";  // otherwise javaCC throws exceptions. An empty string is
                    // returned because the string is accessed by callers.
                    // Nevertheless we're in an error condition, so our only goal
                    // is to reach the end of the file to show as many errors as
                    // possible
    }
}



OperandRegister Register() :
{
    Token t;
}
{
    try {
        t = <REG_8>
        { return new OperandRegister( Register.getRegister8( t.image ) , 1 ); }
      |
        t = <REG_16>
        { return new OperandRegister( Register.getRegister16( t.image ) , 2 ); }
      |
        t = <REG_32>
        { return new OperandRegister( Register.getRegister32( t.image ) , 4 ); }
      |
        t = <REG_64>
        { return new OperandRegister( Register.getRegister64( t.image ) , 8 ); }
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return null; // otherwise javaCC throws exceptions
    }
}

Operand FormatE(int size) :
{
    OperandMemory mem = null;
    OperandRegister reg = null;
}
{
    try {
      (
        reg = FormatG()
        { return reg; }
      |
        mem = FormatM(size)
        { return mem; }
      )
      {
        if(reg != null)
            return reg;
        return mem;
      }

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return null; // otherwise javaCC throws exceptions
    }
}



int FormatK() :
{
    OperandImmediate imm;
}
{
    try {
        imm = ConstantExpression()
        { return (int)imm.getValue(); }
    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return -1; // otherwise javaCC throws exceptions
    }
}



OperandRegister FormatG() :
{
    OperandRegister reg;
}
{
    try {
        reg = Register()
        { return reg; }

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return null; // otherwise javaCC throws exceptions
    }
}

/* Both label and direct address */
OperandMemory FormatM(int size) :
{
    long displacement = 0;
    OperandRegister base = null;
    Token index = null;
    Token scale = null;
}
{
    try {
        (
        LOOKAHEAD(2)
        (displacement = Expression())
        (<LBRACE>
            (
              (
                base = Register()
                ("," index = <REG_64> "," scale = <INTEGER>)?
              )
            |
              ("," index = <REG_64> "," scale = <INTEGER>)
            )
        <RBRACE>)?
        |
        <LBRACE>
            (
              (
                base = Register()
                ("," index = <REG_64> "," scale = <INTEGER>)?
              )
            |
              ("," index = <REG_64> "," scale = <INTEGER>)
            )
        <RBRACE>
        )

        {
             int bs = (base != null ? base.getRegister() : -1);
             int base_size = (base != null ? base.getSize() : -1);
             int idx = (index != null ? Register.getRegister64( index.image ) : -1);
             int scl = (scale != null ? Integer.parseInt(scale.image) : -1);

             return new OperandMemory(bs, base_size, idx, scl, (int)displacement, size);
        }

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return null; // otherwise javaCC throws exceptions
    }
}



Operand FormatB(int size) :
{
    OperandImmediate imm;
    OperandMemory mem;
    OperandRegister reg;
    Token t;
}
{
    try {

        /* ACTUAL RULE */
          imm = ConstantExpression()
          {
            imm.setSize(size); // The size of an immediate depends on the instruction suffix
            return imm;
          }
        |
          reg = FormatG()
          { return reg; }
        |
          mem = FormatM(size)
          { return mem; }

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return null; // otherwise javaCC throws exceptions
    }
}


/***************************************************/
/* Rules to handle simple expressions in constants */
/***************************************************/

OperandImmediate ConstantExpression() :
{
    long value;
}
{
    try {

        /* ACTUAL RULE */
        <CONSTANT>
        value = Expression()
        { return new OperandImmediate(value); }

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return null; // otherwise javaCC throws exceptions
    }
}


long Expression() :
{
    long value;
    long i;
}
{
    try {

        /* ACTUAL RULE */
        value = Term()
        (
            <PLUS> i = Term()
            { value += i; }
        |
            <MINUS> i = Term()
            { value -= i; }
        )*
        { return value; }

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return -1;
    }
}

long Term() :
{
    long value;
    long i;
}
{
    try {

        value = Primary()
        (
            <TIMES> i = Primary()
            { value *= i ; }
        |   <DIVIDE> i = Primary()
            { value /= i; }
        )*
        { return value; }

    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return -1;
    }
}

long Primary() :
{
    Token t;
    long i;
}
{
    try {

        t = <INTEGER>
        { return stringToInt( t.image ); }
      | <LOCATION_COUNTER>
        { return this.program.getLocationCounter(); }
      | t = <LABEL_NAME>
        {
            // Check if there is an equ with this name
            Long value = this.program.getEqu(t.image);
            if(value != null)
                return value;

            // Check if there is a label with this name
            value = this.program.getLabelAddress(t.image);
            if(value != null)
                return value;

            // Otherwise, it could be a label that is defined later in the program
            this.program.addRelocationEntry(this.program.getLocationCounter(), t.image);
            return 0; // This will be resolved later
        }
      | <LBRACE> i = Expression() <RBRACE>
        { return i; }
      | <MINUS> i = Primary()
        { return -i; }


    } catch (ParseException ex) {
        error_recover(ex, NEWLINE);
        return -1;
    }
}
